package analizadores;

import java_cup.runtime.Symbol;
import java.util.LinkedList;
import java.util.HashMap;
import java.util.Map;
import errores.ErrorSintactico;
import instrucciones.*;
import enums.*;



parser code {:
    public Map<String, String> simbolos = new HashMap<>();

    public LinkedList<String> barraPalabras = new LinkedList<>();
    public LinkedList<String> barraValores = new LinkedList<>();
    public String barraTitulo = "";
    public String barraTituloX = "";
    public String barraTituloY = "";

    public LinkedList<String> piePalabras = new LinkedList<>();
    public LinkedList<String> pieValores = new LinkedList<>();
    public String pieTitulo = "";

    public LinkedList<Instruccion> AST;
    public LinkedList<ErrorSintactico> errores = new LinkedList<>();
    public void error_sintactico(Symbol s) {
        if (s.value != null)
            errores.add(new ErrorSintactico(s.value.toString(), s.left, s.right));
    }
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception {
                throw new Exception("Error irrecuperable en (linea, columna): " + s.left + ", " + s.right);
    }
:}

terminal String VOID, MAIN;
terminal String MAYOR, MENOR, MAYOR_IGUAL, MENOR_IGUAL, IGUAL, NO_IGUAL, NOT, OR, AND, PUNTO, INCREMENTO;
terminal String PUNTO_COMA, DOS_PUNTOS;
terminal String SUMA, RESTA, MULTIPLICACION, DIVISION;
terminal String PARENTESIS_IZQ, PARENTESIS_DER, LLAVE_IZQ, LLAVE_DER, COMA, CORCHETE_IZQ, CORCHETE_DER;
terminal String IF, ELSE, SWITCH, CASE, DEFAULT, SWITCH_BREAK, FOR, WHILE, DO, CONSOLE, WRITE;
terminal String NUM, STR, CHAR, TYPEDEF, ID, BOOLEAN;
terminal String DEFINIRGLOBALES, GRAFICABARRAS, DOLAR, NEWVALOR, GRAFICAPIE, TITULOX, TITULOY, TITULO, EJEX, VALORES;

non terminal ini;

non terminal LinkedList<Instruccion> statements;
non terminal Instruccion statement;
non terminal Operacion expr;
non terminal LinkedList<Elif> elifs;
non terminal LinkedList<Case> cases;
non terminal Case case;

non terminal Operacion aritmetic;
non terminal Operacion logical;
non terminal Operacion symbols;
non terminal Operacion unitary;

non terminal If if;

non terminal Instruccion mainstatement;
non terminal LinkedList<Instruccion> mainstatements;

non terminal LinkedList<ImprimirExpresion> impresiones;
non terminal ImprimirExpresion impresion;

non terminal funciones, declaraciones, declaracion, instruccionesBarra, instruccionBarra, asignacionBarra, asignacionesBarra, ejexBarra, ejesxBarra;
non terminal instruccionesPie, instruccionPie, ejesxPie, ejexPie, ejesypie, ejeypie;
precedence left MAYOR, MENOR, MAYOR_IGUAL, MENOR_IGUAL, IGUAL, NO_IGUAL;
precedence left AND, OR;
precedence left SUMA, RESTA;
precedence left MULTIPLICACION, DIVISION;
precedence left PARENTESIS_IZQ;

start with ini;

ini::= VOID MAIN PARENTESIS_IZQ PARENTESIS_DER LLAVE_IZQ  mainstatements:s LLAVE_DER {:parser.AST=s;:}
;

mainstatements ::= mainstatements:s mainstatement:t {:RESULT=s; RESULT.add(t);:}
| mainstatement:s {:RESULT = new LinkedList<Instruccion>(); RESULT.add(s);:}
;


mainstatement ::= statement:s {:RESULT = s;:}
| funciones;

funciones ::= VOID DEFINIRGLOBALES PARENTESIS_IZQ PARENTESIS_DER LLAVE_IZQ declaraciones LLAVE_DER {:System.out.println("Si esta llegando, scoop void definir");:}
| VOID GRAFICABARRAS PARENTESIS_IZQ PARENTESIS_DER LLAVE_IZQ instruccionesBarra LLAVE_DER
| VOID GRAFICAPIE PARENTESIS_IZQ PARENTESIS_DER LLAVE_IZQ instruccionesPie LLAVE_DER
;
// GRAFICAR PIE
instruccionesPie ::= instruccionesPie instruccionPie
| instruccionPie
;
instruccionPie ::= TYPEDEF TITULO IGUAL ID:s PUNTO_COMA {: pieTitulo = simbolos.get(s); :}
| TYPEDEF TITULO IGUAL STR:s PUNTO_COMA {: pieTitulo = s; :}
| TYPEDEF TITULO IGUAL DOLAR LLAVE_IZQ NEWVALOR COMA STR:a COMA STR:b LLAVE_DER PUNTO_COMA  {:pieTitulo = utilidades.GetJSON.getJSON(a,b);:}
| TYPEDEF CORCHETE_IZQ CORCHETE_DER VALORES IGUAL LLAVE_IZQ ejesypie LLAVE_DER PUNTO_COMA
| TYPEDEF CORCHETE_IZQ CORCHETE_DER EJEX IGUAL LLAVE_IZQ ejesxPie LLAVE_DER PUNTO_COMA
;

ejesypie ::= ejesypie COMA ejeypie
| ejeypie
;
ejeypie ::= ID:s {: pieValores.add(simbolos.get(s)); :}
| NUM:s {: pieValores.add(s); :}
| DOLAR LLAVE_IZQ NEWVALOR COMA STR:a COMA STR:b LLAVE_DER {:pieValores.add(utilidades.GetJSON.getJSON(a,b)); :}
;
ejesxPie ::= ejesxPie COMA ejexPie
| ejexPie
;
ejexPie ::= ID:s {: piePalabras.add(simbolos.get(s)); :}
| STR:s {: piePalabras.add(s); :}
| DOLAR LLAVE_IZQ NEWVALOR COMA STR:a COMA STR:b LLAVE_DER {:piePalabras.add(utilidades.GetJSON.getJSON(a,b)); :}
;
// GRAFICAR BARRA

instruccionesBarra ::= instruccionesBarra instruccionBarra
| instruccionBarra
;

instruccionBarra ::= TYPEDEF TITULO IGUAL ID:s PUNTO_COMA {: barraTitulo = simbolos.get(s); :}
                   | TYPEDEF TITULO IGUAL STR:s PUNTO_COMA {: barraTitulo = s; :}
                   | TYPEDEF TITULO IGUAL DOLAR LLAVE_IZQ NEWVALOR COMA STR:a COMA STR:b LLAVE_DER PUNTO_COMA  {:barraTitulo = utilidades.GetJSON.getJSON(a,b);:}
                   | TYPEDEF TITULOX IGUAL ID:s PUNTO_COMA {: barraTituloX = simbolos.get(s); :}
                   | TYPEDEF TITULOX IGUAL STR:s PUNTO_COMA {: barraTituloX = s; :}
                   | TYPEDEF TITULOX IGUAL DOLAR LLAVE_IZQ NEWVALOR COMA STR:a COMA STR:b LLAVE_DER PUNTO_COMA  {:barraTituloX = utilidades.GetJSON.getJSON(a,b);:}
                   | TYPEDEF TITULOY IGUAL STR:s PUNTO_COMA {: barraTituloY = s; :}
                   | TYPEDEF TITULOY IGUAL ID:s PUNTO_COMA {: barraTituloY = simbolos.get(s); :}
                   | TYPEDEF TITULOY IGUAL DOLAR LLAVE_IZQ NEWVALOR COMA STR:a COMA STR:b LLAVE_DER PUNTO_COMA  {:barraTituloY = utilidades.GetJSON.getJSON(a,b);:}
                   | TYPEDEF CORCHETE_IZQ CORCHETE_DER EJEX IGUAL LLAVE_IZQ ejesxBarra LLAVE_DER PUNTO_COMA
                   | TYPEDEF CORCHETE_IZQ CORCHETE_DER VALORES IGUAL LLAVE_IZQ asignacionesBarra LLAVE_DER PUNTO_COMA
;
// EJE X
ejesxBarra ::= ejesxBarra COMA ejexBarra
| ejexBarra
;

ejexBarra ::= ID:s {: barraPalabras.add(simbolos.get(s)); :}
| STR:s {: barraPalabras.add(s); :}
| DOLAR LLAVE_IZQ NEWVALOR COMA STR:a COMA STR:b LLAVE_DER {:barraPalabras.add(utilidades.GetJSON.getJSON(a,b)); :}
;

// EJE Y
asignacionesBarra ::= asignacionesBarra COMA  asignacionBarra
| asignacionBarra
;

asignacionBarra ::= ID:s {: barraValores.add(simbolos.get(s)); :}
| NUM:s {: barraValores.add(s); :}
| DOLAR LLAVE_IZQ NEWVALOR COMA STR:a COMA STR:b LLAVE_DER {:barraValores.add(utilidades.GetJSON.getJSON(a,b)); :}
;
// ---------------------------------------------------------------------------------------------------------------------
declaraciones ::= declaraciones declaracion
| declaracion
;

declaracion ::= TYPEDEF ID:t IGUAL STR:s PUNTO_COMA {:System.out.println("1");simbolos.put(t,s);:}
| TYPEDEF ID:t IGUAL NUM:s PUNTO_COMA {:System.out.println("2");simbolos.put(t,s);:}
| TYPEDEF ID:t IGUAL DOLAR LLAVE_IZQ NEWVALOR COMA STR:a COMA STR:b LLAVE_DER PUNTO_COMA  {:System.out.println("3");simbolos.put(t, utilidades.GetJSON.getJSON(a,b));:}
;

statements ::= statements:s statement:t {:RESULT=s; RESULT.add(t);:}
| statement:s {:RESULT = new LinkedList<Instruccion>(); RESULT.add(s);:};

statement ::= TYPEDEF:n ID:t IGUAL expr:e PUNTO_COMA {:RESULT = new Asignacion(t,n,e);:}
| TYPEDEF:n ID:e PUNTO_COMA {:RESULT = new Declaracion(e,n);:}
| ID:i IGUAL expr:e PUNTO_COMA {:RESULT = new ReAsignacion(i,e);:}
| if:i {:RESULT=i;:}
| FOR PARENTESIS_IZQ TYPEDEF ID:i IGUAL expr:e PUNTO_COMA expr:f PUNTO_COMA ID INCREMENTO PARENTESIS_DER LLAVE_IZQ statements:s LLAVE_DER {:RESULT = new For(i,e,f,s);:}
| WHILE PARENTESIS_IZQ expr:e PARENTESIS_DER LLAVE_IZQ statements:s LLAVE_DER {:RESULT = new While(e,s);:}
| DO LLAVE_IZQ statements:e LLAVE_DER WHILE PARENTESIS_IZQ expr:s PARENTESIS_DER PUNTO_COMA {:RESULT = new DoWhile(e,s);:}
| CONSOLE PUNTO WRITE PARENTESIS_IZQ impresiones:s PARENTESIS_DER PUNTO_COMA {:RESULT = new Imprimir(s);:}
| SWITCH PARENTESIS_IZQ expr:e PARENTESIS_DER LLAVE_IZQ cases:c LLAVE_DER {:RESULT = new Switch(e,c);:}
| SWITCH PARENTESIS_IZQ expr:e PARENTESIS_DER LLAVE_IZQ cases:c DEFAULT DOS_PUNTOS statements:s LLAVE_DER {:RESULT = new Switch(e,c,s);:}
| SWITCH_BREAK PUNTO_COMA {:RESULT = new SentenciaBreak();:}
| error PUNTO_COMA
| error LLAVE_DER
;

impresiones ::= impresiones:s impresion:t {:RESULT=s; RESULT.add(t);:}
| impresion:s {:RESULT = new LinkedList<ImprimirExpresion>(); RESULT.add(s);:}
;

impresion ::=  expr:s SUMA {:RESULT = new ImprimirExpresion(s);:}
| expr:s {:RESULT = new ImprimirExpresion(s);:}
;

if ::= IF PARENTESIS_IZQ expr:e PARENTESIS_DER LLAVE_IZQ statements:s LLAVE_DER {:RESULT = new If(e,s);:}
| IF PARENTESIS_IZQ expr:e PARENTESIS_DER LLAVE_IZQ statements:p LLAVE_DER ELSE LLAVE_IZQ statements:s LLAVE_DER {:RESULT = new If(e,p,s);:}
| IF PARENTESIS_IZQ expr:e PARENTESIS_DER LLAVE_IZQ statements:s LLAVE_DER elifs:f {:RESULT = new If(e,s,f, null);:}
| IF PARENTESIS_IZQ expr:e PARENTESIS_DER LLAVE_IZQ statements:s LLAVE_DER elifs:f ELSE LLAVE_IZQ statements:p LLAVE_DER {:RESULT = new If(e,s,f,p);:}
;

elifs ::= elifs:f ELSE IF PARENTESIS_IZQ expr:e PARENTESIS_DER LLAVE_IZQ statements:s LLAVE_DER {:RESULT = f; RESULT.add(new Elif(e, s));:}
| ELSE IF PARENTESIS_IZQ expr:e PARENTESIS_DER LLAVE_IZQ statements:s LLAVE_DER {:RESULT = new LinkedList<Elif>(); RESULT.add(new Elif(e, s));:}
;
cases ::= cases:s  case:t {:RESULT=s; RESULT.add(t);:}
| case:s {:RESULT = new LinkedList<Case>(); RESULT.add(s);:}
;

case ::= CASE expr:e DOS_PUNTOS statements:s {:RESULT = new Case(e,s, false);:}
|CASE expr:e DOS_PUNTOS statements:s SWITCH_BREAK {:RESULT = new Case(e,s, true);:}
;

expr ::= symbols:s {:RESULT = s;:}
| unitary:u {:RESULT = u;:}
| aritmetic:a {:RESULT = a;:}
| logical:l {:RESULT = l;:}
| PARENTESIS_IZQ expr:e PARENTESIS_DER {:RESULT = new Operacion(e);:};

aritmetic ::= expr:a SUMA expr:b {:RESULT = new Operacion(a, EnumOperaciones.SUMA, b);:}
| expr:a RESTA expr:b {:RESULT = new Operacion(a, EnumOperaciones.RESTA, b);:}
| expr:a MULTIPLICACION expr:b {:RESULT = new Operacion(a, EnumOperaciones.MULTIPLICACION, b);:}
| expr:a DIVISION expr:b {:RESULT = new Operacion(a, EnumOperaciones.DIVISION, b);:}
| NUM:n  {: RESULT = new Operacion(n, EnumTerminals.NUM); :};

logical ::= expr:a MAYOR expr:b {:RESULT = new Operacion(a, EnumOperaciones.MAYOR, b);:}
| expr:a MENOR expr:b {:RESULT = new Operacion(a, EnumOperaciones.MENOR, b);:}
| expr:a MAYOR_IGUAL expr:b {:RESULT = new Operacion(a, EnumOperaciones.MAYOR_IGUAL, b);:}
| expr:a MENOR_IGUAL expr:b {:RESULT = new Operacion(a, EnumOperaciones.MENOR_IGUAL, b);:}
| expr:a IGUAL expr:b {:RESULT = new Operacion(a, EnumOperaciones.IGUAL, b);:}
| expr:a NO_IGUAL expr:b {:RESULT = new Operacion(a, EnumOperaciones.NO_IGUAL, b);:}
| expr:a AND expr:b {:RESULT = new Operacion(a, EnumOperaciones.AND, b);:}
| expr:a OR expr:b {:RESULT = new Operacion(a, EnumOperaciones.OR, b);:}
| BOOLEAN:b {:RESULT = new Operacion(b, EnumTerminals.BOOL);:}
;

symbols ::= ID:i {:RESULT = new Operacion(i, EnumTerminals.ID);:}
| STR:s {:RESULT = new Operacion(s, EnumTerminals.STR);:}
| CHAR:c {:RESULT = new Operacion(c, EnumTerminals.CHAR);:} ;

unitary ::= RESTA expr:e {:RESULT = new Operacion(e, EnumUnitaryOperations.NEGATIVE);:}
| NOT expr:e {:RESULT = new Operacion(e, EnumUnitaryOperations.NOT);:};

